
process JUMBLE_RUN {
    tag "$meta.id"
    label 'process_medium'

    conda "${moduleDir}/environment.yml"
    container "docker://sarathmurugan01/jumble:v1.0.0"

    input:
    tuple val(meta), path(bam), path(bai)
    tuple val(meta2), path(jumble_ref)

    output:
    tuple val(meta), path("*.cns"),                 emit: cns
    tuple val(meta), path("*.cnr"),                 emit: cnr
    tuple val(meta), path("*_dnacopy.seg"),         emit: seg
    tuple val(meta), path("*_profile_bedgraph"),    emit: profile_bedgraph
    tuple val(meta), path("*_segments_bedgraph"),   emit: segments_bedgraph
    tuple val(meta), path("*.RDS"),                 emit: rds , optional: true
    path  "versions.yml"          ,                 emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"

    """

    # Run the tool
    jumble-run.R \\
        $args \\
        -r ${jumble_ref} \\
        -b ${bam} \\
        -o "./"

    ## Convert to bedgraph for IGV visualization
    awk -F'\\t' -v OFS='\\t' '\$1 != "chromosome" {print \$1"\\t"\$2"\\t"\$3"\\t"\$6}' \\
         ${prefix}.cnr > ${prefix}_profile_bedgraph

    awk -F'\\t' -v OFS='\\t' '\$1 != "chromosome" {print \$1"\\t"\$2"\\t"\$3"\\t"\$5}' \\
         ${prefix}.cns > ${prefix}_segments_bedgraph

    # Capture versions
    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        jumble-run.R: \$( jumble-run.R --version 2>&1 | head -n 1 || echo "unknown" )
    END_VERSIONS
    """

    stub:
    def prefix = task.ext.prefix ?: "${meta.id}"

    """
    touch ${prefix}.counts.RDS
    touch ${prefix}.cns
    touch ${prefix}.cnr
    touch ${prefix}_dnacopy.seg
    touch ${prefix}_profile_bedgraph
    touch ${prefix}_segments_bedgraph

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        jumble-run.R: "stub"
        R: \$( R --version | sed -n '1p' )
    END_VERSIONS
    """
}
